apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

// Copy the signing.properties.sample file to ~/.sign/signing.properties and adjust the values.
final File PROD_PROPS_FILE = new File(System.getProperty('user.home'), '.sign/signing.properties')
final File REPO_PROPS_FILE = new File('repo.properties')
final Properties PROD_PROPS = loadProperties(PROD_PROPS_FILE)
final Properties REPO_PROPS = loadProperties(REPO_PROPS_FILE)

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.1"

    defaultConfig {
        applicationId "com.politicl"
        minSdkVersion 15
        targetSdkVersion 24
        versionCode 1
        versionName "1.0.1"
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
        vectorDrawables.useSupportLibrary = true
    }

    sourceSets {
        test {
            java.srcDirs += 'src/testlib/java'
        }
        androidTest {
            java.srcDirs += 'src/testlib/java'
        }
    }

    signingConfigs {
        prod {
            setSigningConfigKey(prod, PROD_PROPS)
        }
        debug {
            setSigningConfigKey(debug, REPO_PROPS)
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFile 'test-proguard-rules.pro'
        }
    }

    // while we still have lint errors; remove once those are fixed
    lintOptions {
        disable 'MissingTranslation'
        disable 'InvalidPackage' // required by Butter Knife
        warning 'MissingQuantity'
        warning 'ImpliedQuantity'
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        // required by Butter Knife

        // For Espresso testing libraries. See http://stackoverflow.com/q/33800924/970346.
        exclude 'META-INF/maven/com.google.guava/guava/pom.xml'
        exclude 'META-INF/maven/com.google.guava/guava/pom.properties'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    String supportVersion = '24.+'
    String retrofitVersion = '2.0.2'
    String butterKnifeVersion = '8.0.1'
    String frescoVersion = '0.10.0'
    String okHttpVersion = '3.3.1'
    String testingSupportVersion = '0.5'
    String espressoVersion = '2.2.2'

    compile "com.android.support:appcompat-v7:$supportVersion" // includes support-v4
    compile "com.android.support:design:$supportVersion"
    compile "com.android.support:recyclerview-v7:$supportVersion"
    compile "com.android.support:cardview-v7:$supportVersion"

    compile "com.facebook.fresco:animated-gif:$frescoVersion"
    compile "com.facebook.fresco:fresco:$frescoVersion"
    compile "com.facebook.fresco:imagepipeline-okhttp3:$frescoVersion"
    compile "com.squareup.retrofit2:retrofit:$retrofitVersion"
    compile "com.squareup.retrofit2:converter-gson:$retrofitVersion"

    compile "com.squareup.okhttp3:okhttp-urlconnection:$okHttpVersion" // for JavaNetCookieJar
    compile "com.squareup.okhttp3:logging-interceptor:$okHttpVersion"

    compile 'com.google.code.gson:gson:2.6.2'
    compile "com.github.kevinsawicki:http-request:5.6"

    compile 'org.apache.commons:commons-lang3:3.4'
    compile "com.android.support:palette-v7:$supportVersion"

    compile "com.jakewharton:butterknife:$butterKnifeVersion"

    compile 'com.github.ryanjohn1:onboarding:1.0.3'

    apt "com.jakewharton:butterknife-compiler:$butterKnifeVersion"

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.9.5'
    testCompile 'org.robolectric:robolectric:3.1'
    testCompile "com.squareup.okhttp3:mockwebserver:$okHttpVersion"

    // Required by Android JUnit Runner.
    androidTestCompile "com.android.support:support-annotations:$supportVersion"
    androidTestCompile "com.android.support.test:rules:$testingSupportVersion" // JUnit Rules
    androidTestCompile "com.android.support.test:runner:$testingSupportVersion"
    // Android JUnit Runner
    androidTestCompile "com.android.support.test.espresso:espresso-core:$espressoVersion"
    androidTestCompile 'com.squareup.spoon:spoon-client:1.5.3'
}

private setSigningConfigKey(config, Properties props) {
    if (props) {
        config.storeFile = props['keystore'] == null ? null : file(props['keystore'])
        config.storePassword = props['store.pass']
        config.keyAlias = props['key.alias']
        config.keyPassword = props['key.pass']
    }
    return config
}

@Nullable
private Properties loadProperties(File file) {
    Properties props = null
    if (file.canRead()) {
        props = new Properties()
        props.load(new FileInputStream(file))
    } else {
        System.err.println "\"${file}\" not found"
    }
    return props
}
